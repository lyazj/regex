cmake_minimum_required(VERSION 3.14)
project(regex VERSION 0.0.1)

# 设置项目包含目录、库目录和可执行文件目录
include_directories(${PROJECT_SOURCE_DIR})
link_directories(${PROJECT_SOURCE_DIR})
set(CMAKE_ARCHIVE_OUTPUT_DIRECTORY ${CMAKE_SOURCE_DIR})
set(CMAKE_LIBRARY_OUTPUT_DIRECTORY ${CMAKE_SOURCE_DIR})
set(CMAKE_RUNTIME_OUTPUT_DIRECTORY ${CMAKE_SOURCE_DIR})

# 优化、调试和通用编译选项
if(CMAKE_C_COMPILER_FRONTEND_VARIANT MATCHES GNU)
option(ENABLE_SANITIZER "compile and link with sanitizing flags" OFF)
option(ENABLE_GPROF "compile and link with gprof flags" OFF)
if(NOT CMAKE_BUILD_TYPE)
    set(CMAKE_BUILD_TYPE DEBUG)
endif()
if(CMAKE_BUILD_TYPE STREQUAL DEBUG)
    add_compile_options(-O0 -fno-omit-frame-pointer)
    if(ENABLE_SANITIZER)
        set(SANITIZER_OPTIONS -fno-sanitize-recover=all)
        list(APPEND SANITIZER_OPTIONS -fsanitize=address)
        list(APPEND SANITIZER_OPTIONS -fsanitize=leak)
        list(APPEND SANITIZER_OPTIONS -fsanitize=undefined)
        list(APPEND SANITIZER_OPTIONS -fsanitize=float-divide-by-zero)
        add_compile_options(${SANITIZER_OPTIONS})
        add_link_options(${SANITIZER_OPTIONS})
    endif()
else()
    add_compile_options(-O3)
endif()
if(ENABLE_GPROF)
    set(GPROF_OPTIONS -pg)
    add_compile_options(${GPROF_OPTIONS})
    add_link_options(${GPROF_OPTIONS})
endif()
add_compile_options(-Wall -Wshadow -Wextra -Werror -pedantic -std=c99 -Dinline=__inline__ -Werror=declaration-after-statement)
endif()
if(CMAKE_C_COMPILER_FRONTEND_VARIANT MATCHES MSVC)
    add_compile_options(/wd4146)  # unary minus operator applied to unsigned type
endif()

# 库文件生成
option(ENABLE_SHARED "build shared library" ON)
if(CMAKE_C_COMPILER_FRONTEND_VARIANT MATCHES MSVC)
    set(ENABLE_SHARED OFF)  # [XXX] Current issue: .dll generated but .lib used to link
endif()
file(GLOB SOURCE_FILES CONFIGURE_DEPENDS regex/*.c)
if(ENABLE_SHARED)
if(CMAKE_C_COMPILER_FRONTEND_VARIANT MATCHES GNU)
    add_compile_definitions(ENABLE_VISIBILITY)
endif()
    add_library(regex SHARED ${SOURCE_FILES})
if(CMAKE_C_COMPILER_FRONTEND_VARIANT MATCHES GNU)
    target_compile_options(regex PRIVATE -fvisibility=hidden)
endif()
else()
    add_library(regex ${SOURCE_FILES})
endif()

# 可执行文件生成
file(GLOB SOURCE_FILES CONFIGURE_DEPENDS test/*.c)
foreach(SOURCE_FILE ${SOURCE_FILES})
    get_filename_component(EXECUTABLE_NAME ${SOURCE_FILE} NAME_WLE)
    get_filename_component(EXECUTABLE_DIRECTORY ${SOURCE_FILE} DIRECTORY)
    add_executable(${EXECUTABLE_NAME} ${SOURCE_FILE})
    set_target_properties(${EXECUTABLE_NAME} PROPERTIES RUNTIME_OUTPUT_DIRECTORY ${EXECUTABLE_DIRECTORY})
    target_link_libraries(${EXECUTABLE_NAME} PRIVATE regex)
endforeach()
